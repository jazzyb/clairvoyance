;;;; 8-puzzle

(role player)

;;;; base & input propositions

(<= (base (cell ?m ?n ?t)) (index ?m) (index ?n) (tile ?t))
(<= (base (step ?x)) (successor ?y ?x))
(base (step 0))

(index 1)
(index 2)
(index 3)

(tile 1)
(tile 2)
(tile 3)
(tile 4)
(tile 5)
(tile 6)
(tile 7)
(tile 8)
(tile b)

(<= (input player (move ?m ?n)) (index ?m) (index ?n))

;;;; initial state

(init (cell 1 1 b))
(init (cell 1 2 1))
(init (cell 1 3 3))
(init (cell 2 1 4))
(init (cell 2 2 2))
(init (cell 2 3 5))
(init (cell 3 1 7))
(init (cell 3 2 8))
(init (cell 3 3 6))
(init (step 0))

;;;; adjacency

(succ 1 2)
(succ 2 3)
(pred 2 1)
(pred 3 2)

;;;; legal moves: e.g.: move(2,2) means move the blank square to 2,2 from whereever it is.
;;;; this will only be legal if it was in a square adjacent to the square picked

(<= (legal player (move ?x ?y))
    (true (cell ?u ?y b))
    (or (succ ?x ?u) (pred ?x ?u)))

(<= (legal player (move ?x ?y))
    (true (cell ?x ?v b)) 
    (or (succ ?y ?v) (pred ?y ?v)))

;;;; next states

(<= (next (step ?x))
    (true (step ?y))
    (successor ?y ?x))
(<= (next (cell ?x ?y b))
    (does player (move ?x ?y))) 

(<= (next (cell ?u ?y ?z))
    (does player (move ?x ?y)) 
    (true (cell ?u ?y b))
    (true (cell ?x ?y ?z))
    (distinct ?z b))

(<= (next (cell ?x ?v ?z))
    (does player (move ?x ?y)) 
    (true (cell ?x ?v b)) 
    (true (cell ?x ?y ?z))
    (distinct ?z b))

(<= (next (cell ?u ?v ?z))
    (true (cell ?u ?v ?z))
    (does player (move ?x ?y))
    (or (distinct ?x ?u) (distinct ?y ?v))
    (true (cell ?x1 ?y1 b))
    (or (distinct ?x1 ?u) (distinct ?y1 ?v)))

;;;; goal and terminal

(<= (goal player 100)
    inorder
    (true (step 4)))

(<= (goal player 99) 
    inorder
    (true (step ?x))
    (distinct ?x 4))

(<= (goal player 0) 
    (not inorder))

(<= terminal inorder)
(<= terminal
    (true (step 6)))

(<= inorder
    (true (cell 1 1 1)) 
    (true (cell 1 2 2)) 
    (true (cell 1 3 3)) 
    (true (cell 2 1 4)) 
    (true (cell 2 2 5)) 
    (true (cell 2 3 6)) 
    (true (cell 3 1 7)) 
    (true (cell 3 2 8)) 
    (true (cell 3 3 b)))

;;; Step counting
  (successor 0 1)
  (successor 1 2)
  (successor 2 3)
  (successor 3 4)
  (successor 4 5)
  (successor 5 6)
